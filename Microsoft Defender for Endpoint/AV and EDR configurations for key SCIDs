//This script offers information on the AV and EDR configurations for key SCIDs on both Windows and Linux endpoints.

DeviceTvmSecureConfigurationAssessment
| where ConfigurationId in ('scid-91', 'scid-6090', 'scid-2000', 'scid-92', 'scid-6094', 'scid-6091', 'scid-2001', 'scid-2002', 'scid-2003', 'scid-2010', 'scid-2011', 'scid-2012', 'scid-2013', 'scid-2014', 'scid-2030', 'scid-6001', 'scid-6095', 'scid-6002', 'scid-2016')
| extend Test = case(
    ConfigurationId == "scid-2000", "SensorEnabled",
    ConfigurationId == "scid-6090", "(Linux)RealTimeProtectionStatus",
    ConfigurationId == "scid-2001", "SensorDataCollection",
    ConfigurationId == "scid-6001", "(Linux)SensorDataCollection",
    ConfigurationId == "scid-2002", "ImpairedCommunications",
    ConfigurationId == "scid-2003", "TamperProtection",
    ConfigurationId == "scid-2010", "AntivirusEnabled",
    ConfigurationId == "scid-2011", "AntivirusSignatureVersion",
    ConfigurationId == "scid-2012", "RealtimeProtection",
    ConfigurationId == "scid-91", "BehaviorMonitoring",
    ConfigurationId == "scid-2013", "PUAProtection",
    ConfigurationId == "scid-2014", "AntivirusReporting",
    ConfigurationId == "scid-92", "ScanEnabledforDownloadedFiles",
    ConfigurationId == "scid-6094", "(Linux)CloudProtection",
    ConfigurationId == "scid-6091", "(Linux)PUAProtection",
    ConfigurationId == "scid-2030", "UpdateMDECoreComponents",
    ConfigurationId == "scid-6095", "(Linux)AntiVirusDefinitionVersion",
    ConfigurationId == "scid-6002", "(Linux)ImpairedCommunications",
    ConfigurationId == "scid-2016", "CloudProtection",
    "N/A"),
    Result = case(IsApplicable == 0, "N/A", IsCompliant == 1, "GOOD", "BAD")
| extend packed = pack(Test, Result)
| summarize Tests = make_bag(packed), DeviceName = any(DeviceName), OSPlatform = any(OSPlatform) by DeviceId
| evaluate bag_unpack(Tests)
